import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    // for run ./gradlew jmh
    id 'me.champeau.jmh' version '0.6.8'
    id "org.owasp.dependencycheck" version "8.3.1"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'kotlin-kapt'

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenCentral()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.4.0')

    implementation "com.exactpro.th2:common:5.3.0-separate-executor-+" //FIXME: migrate to release
    implementation "com.exactpro.th2:common-utils:2.1.1-transport-protocol-5840755567-510df64-SNAPSHOT"
    implementation "com.exactpro.th2:codec:5.3.0-new-proto-+" //FIXME: migrate to release
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    implementation 'io.netty:netty-buffer'

    implementation 'org.slf4j:slf4j-api'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'io.github.microutils:kotlin-logging:3.0.0'

    compileOnly 'com.google.auto.service:auto-service:1.0.1'

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.3"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:1.6.21'

    kapt 'com.google.auto.service:auto-service:1.0.1'

    jmh 'org.openjdk.jmh:jmh-core:1.36'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'

    implementation 'org.apache.avro:avro:1.11.1'
    implementation 'commons-io:commons-io:2.12.0'
}

test {
    useJUnitPlatform()
}

application {
    mainClass.set('com.exactpro.th2.codec.MainKt')
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}
jmh {
    //profilers = ['stack'] // Use profilers to collect additional data. Supported profilers: [cl, comp, gc, stack, perf, perfnorm, perfasm, xperf, xperfasm, hs_cl, hs_comp, hs_gc, hs_rt, hs_thr, async]
}
dependencyLocking {
    lockAllConfigurations()
}
dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}
